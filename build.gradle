plugins {
    id 'java'
    // maven 发布插件
    id 'maven-publish'
    // 签名插件
    id 'signing'
}

group 'com.github.wujiuye'
version '1.3.21-RELEASE'

repositories {
    mavenCentral()
}

dependencies {
    // excel表格
    compile group: 'org.apache.poi', name: 'poi', version: '3.17'
    compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'
    compile group: 'org.apache.poi', name: 'poi-ooxml-schemas', version: '3.17'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// 打包源码
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

// 生成 javadoc jar
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        // 推送
        publishMyProjectMiniexcel(MavenPublication) {
            // 编译jar包
            from components.java
            // 打包源码
            artifact sourcesJar
            // javadoc
            artifact javadocJar

            pom {
                name = "miniexcel"
                description = "基于poi封装的一套excel文件读写工具，完美解决oom问题。优雅的使用设计模式提供更简单的使用姿势！更有详细的使用教程。如需获取帮助，关注微信公众号:Java艺术"
                url = "https://github.com/wujiuye/miniexcel"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "wujiuye"
                        name = "wujiuye"
                        email = "419611821@qq.com"
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/wujiuye/miniexcel.git"
                    developerConnection = "scm:git:https://github.com/wujiuye/miniexcel.git"
                    url = "https://github.com/wujiuye/miniexcel"
                }
            }
        }
    }
    // 到 https://oss.sonatype.org/#nexus-search; 搜索是否上传成功
    repositories {
        // 发布Release版本可在版本号后面带上'-RELEASE'，也可不带
        maven {
            name 'Release'
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
        // 发布Snapshot快照版本必须在版本号后面加上'-SNAPSHOT'
        maven {
            name = 'Snapshot'
            url = 'https://oss.sonatype.org/content/repositories/snapshots'
            credentials {
                username = "${NEXUS_USERNAME}"
                password = "${NEXUS_PASSWORD}"
            }
        }
    }
}

// 必须在 publishing 配置之后
signing {
    sign publishing.publications.publishMyProjectMiniexcel
}

